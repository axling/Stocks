<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd" >
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en" >
  <head>
    <title>Stocks</title>
    <meta http-equiv="content-type" content="text/html;charset=utf-8" />
    <link rel="stylesheet" href="css/layout.css" type="text/css" />
    <link rel="stylesheet" href="css/menu.css" type="text/css" />
    <link rel="stylesheet" href="css/tables.css" type="text/css" />
    <link rel="stylesheet" href="css/general.css" type="text/css" />
  </head>
  <body>
    <div id="pagewidth" >
      <div id="header" >
	<div id="headline">Stocks</div>
	<erl>
out(A) ->
    {ehtml, [{ul, [{class, "menu"}], 
	      [{li, [], 
		[{a, [{href, "index.yaws"}], [{span, [], ["Start"]}]}]},
	       {li, [], 
		[{a, [{href, "stock.yaws"}, {class, active}], [{span, [], ["Stocks"]}]}]}]}]}.
	</erl>
      </div>
      <div id="wrapper" class="clearfix" >
	<div id="twocols" class="clearfix"> 
	  <div id="maincol" >
	    <erl>
-include("include/mnesia_defs.hrl").
-include_lib("stdlib/include/qlc.hrl").
out(A) ->
    QueryList = yaws_api:parse_query(A),
    PostList = yaws_api:parse_post(A),
    case proplists:get_value("name", QueryList) of
	Instrument when not is_atom(Instrument) ->
	    
	    PageSize = list_to_integer(proplists:get_value("page_size", QueryList, "15")),
	    PageNr = list_to_integer(proplists:get_value("page", QueryList, "1")),
	    
	    QhCompany = db_handler:get_query_handle(company),
	    [Company] = db_handler:q(qlc:q([C#company.name || C <- QhCompany, 
							      C#company.instrument==Instrument])),
	    %% Here I assume that we are running yaws from the root directory
	    QhStocks = db_handler:get_query_handle(stocks),
	    StockQuery = qlc:q([{Stock#stocks.date, Stock#stocks.closing} ||
				   Stock <- QhStocks, 
				   Stock#stocks.company == Company]),
	    
	    Stocks = lists:sort(
		       fun({A, _}, {B, _}) ->
			       date_lib:is_greater(B, A)
		       end, db_handler:q(StockQuery)),
	    QueryDays = list_to_integer(proplists:get_value("days", PostList, "30")),
	    Days = if
		      QueryDays > length(Stocks) - 30 ->
			   length(Stocks) - 30;
		       true ->
			   QueryDays
		   end,
	    
	    
	    TempStocks = lists:nthtail(length(Stocks)-Days, Stocks),
	    {PageStocks, PaginateLinks} = get_page_stocks(lists:reverse(TempStocks), 
							  PageSize, PageNr, QueryList),	    
	    TempStocksMvgAvg1 = lists:nthtail(length(Stocks)-Days-10, Stocks),
	    TempStocksMvgAvg2 = lists:nthtail(length(Stocks)-Days-30, Stocks),
	    MvgAvg1 = calculate_moving_average([Val || {_, Val} <- TempStocksMvgAvg1], 10),
	    MvgAvg2 = calculate_moving_average([Val || {_, Val} <- TempStocksMvgAvg2], 30),
	    StdErrMonth = std_err([S1 || {_,S1} <- TempStocks]),
	    StdErrAll = std_err([S2 || {_,S2} <- Stocks]),
	    AvgPriceTemp = lists:sum([S1 || {_,S1} <- TempStocks])/length(TempStocks),
	    AvgPrice = lists:sum([S1 || {_,S1} <- Stocks])/length(Stocks),
	    MaxValueTemp = lists:max([Val || {_, Val} <- TempStocks]),
	    MaxValue = lists:max([Val || {_, Val} <- Stocks]),
	    MinValueTemp = lists:min([Val || {_, Val} <- TempStocks]),
	    MinValue = lists:min([Val || {_, Val} <- Stocks]),
	    ChartString = chart_builder:build_chart(
			    Company, [{[Value || {_, Value} <- TempStocks], Company, {"000000", {1,2,0}}}, 
				      {MvgAvg1, "10 Day Moving Average", {"0000FF", {3,6,6}}},
				      {MvgAvg2, "30 Day Moving Average", {"AA2277", {3,15,12}}}],
			    [Date || {Date, _} <- TempStocks],
			    Days, 400, 220),
	    
	    %% Remove all values from stocks that are older than 30 days
	    {ehtml, [{img, [{src, ChartString}, {alt, "Chart"}]},
		     {table, [], 
		      [{tr, [], 
			[{th, [], []}, {th, [], [integer_to_list(Days) ++ " days"]}, {th, [], ["All Time"]}]},
		       {tr, [], 
			[{td, [], ["Variation Coefficient"]}, 
			 {td, [], [f("~.2f%", [StdErrMonth*100])]},
			 {td, [], [f("~.2f%", [StdErrAll*100])]}]},
		       {tr, [], 
			[{td, [], ["Average Price"]}, 
			 {td, [], [f("~.2f", [AvgPriceTemp])]},
			 {td, [], [f("~.2f", [AvgPrice])]}]},
		       {tr, [], 
			[{td, [], ["Max Value"]}, 
			 {td, [], [f("~.2f", [MaxValueTemp])]},
			 {td, [], [f("~.2f", [MaxValue])]}]},
		       {tr, [], 
			[{td, [], ["Min Value"]}, 
			 {td, [], [f("~.2f", [MinValueTemp])]},
			 {td, [], [f("~.2f", [MinValue])]}]}]},		     
		     {table, [{class, "stock_table"}], 
		      [{tr, [], [{th, [{colspan, 2}], [yaws_api:htmlize(Company) ++ ", " ++  
						       integer_to_list(Days) ++ " days"]}]},
		       {tr, [], [{td, [{class, "width"}], "Date"}, {td, [{class, "adjacent"}], "Value"}]}] ++ 
		      lists:map(
			fun({Date, Value}) ->
				{tr, [], [{td, [], [date_lib:convert_date_e_s(Date)]},
					  {td, [{class, "adjacent"}], f("~p", [Value])}]}
			end, PageStocks) ++ 
		     [{tr, [], 
		       [{td, [{class, "paginate_links"},{colspan, 2}], 
			 PaginateLinks
			 }]}]
		     }]};	       	
	undefined ->
	    Qh = db_handler:get_query_handle(company),
	    Query = qlc:q([Company || Company <- Qh]),
	    Companies = lists:sort(db_handler:q(Query)),
	    {ehtml, [{h1, [], ["Stocks"]},
		     {ul, [], 
		      lists:map(fun(#company{instrument=Instrument, name=Name}) ->
					{li, [], [{a, [{href, "stock.yaws?name=" ++ Instrument}], 
						   [yaws_api:htmlize(Name)]}]}
				end, Companies)}]}
    end.


calculate_moving_average(StockValues, Days) when Days < length(StockValues) -> 
    calculate_moving_average(StockValues, 1, Days, []).

calculate_moving_average(StockValues, Start, Days, AccValues) 
  when (Start+Days-1) < length(StockValues) ->
    MvgAvg = lists:sum(lists:sublist(StockValues, Start, Days))/Days,
    calculate_moving_average(StockValues, Start+1, Days, [MvgAvg | AccValues]);
calculate_moving_average(_StockValues, _Start, _Days, AccValues) ->
    lists:reverse(AccValues).

calculate_cumulative_average(Values, Start) ->
    {List1, RestList} = lists:split(Start+1, Values),
    CumSum = lists:sum(List1)/length(List1),
    cum_avg(RestList, [CumSum], Start+1).

cum_avg([], AccVal, _) ->
    lists:reverse(AccVal);
cum_avg([First | Rest], [CA | _Rest]=AccVal, I) ->
    Sum = (First + CA*(I-1))/I,
    cum_avg(Rest, [Sum | AccVal], I+1).

get_page_stocks(Stocks, PageSize, PageNr, QueryList) ->
    LastPage = trunc(length(Stocks)/PageSize),
    if 
	((PageNr =< 1) and (Stocks /= [])) ->
	    PageStocks1 = lists:sublist(Stocks, PageSize),
	    Html = 
		[{a, [{href, make_page_link(2, QueryList)}], [">"]},
		 "   ",
		 {a, [{href, make_page_link(LastPage, QueryList)}], [">>"]}],
	    {PageStocks1, Html};
	
	(((PageNr - 1) * PageSize) < length(Stocks)) ->
	    PageStocks2 = lists:sublist(Stocks, ((PageNr-1)*PageSize), PageSize),
	    Html = 
		[{a, [{href, make_page_link(1, QueryList)}], ["<<"]},
		 "   ",
		 {a, [{href, make_page_link(PageNr-1, QueryList)}], ["<"]},
		 "   ",
		 {a, [{href, make_page_link(PageNr+1, QueryList)}], [">"]},		 
		 "   ",
		 {a, [{href, make_page_link(LastPage, QueryList)}], [">>"]}],
	    {PageStocks2, Html};
	true  ->
	    PageStocks3 = lists:sublist(Stocks, ((LastPage-1)*PageSize), PageSize),
	    Html = 
		[{a, [{href, make_page_link(1, QueryList)}], ["<<"]},
		 "   ",
		 {a, [{href, make_page_link(PageNr-1, QueryList)}], ["<"]}],
	    {PageStocks3, Html}
    end.
	    
make_page_link(Page, QueryList) ->
    NewQueryList = lists:keystore("page", 1, QueryList, {"page", integer_to_list(Page)}),
    "stock.yaws?" ++ make_query_string(NewQueryList).
					     
make_query_string(QueryList) ->
    string:join(
      lists:map(
	fun({Key, Val}) ->
		lists:concat([Key, "=", Val])
	end, QueryList),
     "&amp;").

std_err(DataSet) ->
    Avg = lists:sum(DataSet)/length(DataSet),
    SqrSum = 
	lists:sum(
	  lists:map(
	    fun(Val) ->
		    math:pow(Avg - Val, 2)
	    end, DataSet)),
    math:sqrt(SqrSum/(length(DataSet) -1))/Avg.

	</erl>
        </div>
<div id="rightcol" ></div>
	    </div> 
<div id="leftcol" ></div>
</div>
	<div id="footer" >
	</div>
	</div>
</body>

</html>
